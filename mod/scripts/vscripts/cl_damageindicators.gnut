global function Cl_DamageIndicator_Init
global function PlayerDidDamageText
// ()
const vector OUTGOING_DAMAGE_TEXT_BEGIN_POS = <0.525, 0.4, 0>
float OUTGOING_DAMAGE_TEXT_STACKING_FADE_TIME = 0.5
float OUTGOING_DAMAGE_TEXT_STACKING_DURATION = 1.3
float OUTGOING_DAMAGE_TEXT_STACKING_STAY_TIME = 0.8

const vector INCOMING_DAMAGE_TEXT_BEGIN_POS = <0.63, 0.5 - 0.3, 0>
float INCOMING_DAMAGE_TEXT_STACKING_FADE_TIME = 0.5
float INCOMING_DAMAGE_TEXT_STACKING_DURATION = 1.3
float INCOMING_DAMAGE_TEXT_STACKING_STAY_TIME = 0.8

var stackingOutgoingDamageTextRui
float accumulatedOutgoingDamage = 0.0
float lastOutgoingDamageTextHitTime = 0.0

var stackingIncomingDamageTextRui
float accumulatedIncomingDamage = 0.0
float lastIncomingDamageTextHitTime = 0.0
bool isMenuOpen = false

void function Cl_DamageIndicator_Init()
{
	foreach ( k, v in eDamageSourceId )
	{
		AddLocalPlayerTookDamageCallback( v, PlayerTookDamage )
	}
	#if MP
		//ClPilotSpeedometer_Init()
	#endif
    thread Thread_StackingDamageFlyoutText()
}

void function PlayerTookDamage( float damage, vector pos, int damageType, int damageSourceId, entity attacker )
{
	if (!GetLocalViewPlayer().IsTitan() || !GetConVarBool("comp_hud_incoming_damage_indicator")) return
	if (Time() - lastIncomingDamageTextHitTime > GetConVarFloat("comp_hud_incoming_damage_indicator_duration"))
		accumulatedIncomingDamage = damage
	else accumulatedIncomingDamage += damage
	lastIncomingDamageTextHitTime = Time() // :D
	RuiSetString( stackingIncomingDamageTextRui, "msgText", format( "-%i", int( accumulatedIncomingDamage ) ) )
}

void function SetOutgoingDamageTextRui(float damage, bool isCrit, entity victim)
{
	if (stackingOutgoingDamageTextRui == null) return
	RuiSetString( stackingOutgoingDamageTextRui, "msgText", format("%i%s", accumulatedOutgoingDamage.tointeger(), CalculateVictimHP(damage, victim)))
	float msgFontSize = GraphCapped( accumulatedOutgoingDamage, victim.IsTitan() ? 500 : 50, victim.IsTitan() ? 10000 : 200, 36, victim.IsTitan() ? 56 : 48 )
	RuiSetFloat( stackingOutgoingDamageTextRui, "msgFontSize", msgFontSize)
	RuiSetFloat2( stackingOutgoingDamageTextRui, "msgPos", GetConVarFloat2("comp_hud_damage_indicator_pos") - <0, GraphCapped(msgFontSize, 36, 72, 0.0, 0.05), 0> )
	RuiSetFloat3( stackingOutgoingDamageTextRui, "msgColor", GetDamageTextColor(isCrit, victim) )
}

void function AddSmallDamageTextFlyout(float damage, bool isCrit, entity victim)
{
	var rui = RuiCreate( $"ui/cockpit_console_text_top_left.rpak", clGlobal.topoCockpitHudPermanent, RUI_DRAW_COCKPIT, 0)
	RuiSetInt( rui, "maxLines", 1 );
	RuiSetInt( rui, "lineNum", 1 );
	RuiSetFloat2( rui, "msgPos", GetConVarFloat2("comp_hud_damage_indicator_pos") )
	RuiSetString( rui, "msgText",  damage.tointeger().tostring() )
	RuiSetFloat3( rui, "msgColor",  GetDamageTextColor(isCrit, victim))
	RuiSetFloat( rui, "msgFontSize", 24.0)
	RuiSetFloat( rui, "msgAlpha", 0.5 )
	RuiSetFloat( rui, "thicken", 0.0 )

	float startTime = Time()
	float OUTGOING_DAMAGE_TEXT_SMALL_FADE_TIME = 0.5

	while ( Time() - startTime < OUTGOING_DAMAGE_TEXT_SMALL_FADE_TIME )
	{
		float alpha = Graph( Time() - startTime, 0, OUTGOING_DAMAGE_TEXT_SMALL_FADE_TIME, 0.5, 0 )
		vector posOffset = GetConVarFloat2("comp_hud_damage_indicator_pos")
		posOffset.y += Graph( Time() - startTime, 0, OUTGOING_DAMAGE_TEXT_SMALL_FADE_TIME, 0, -0.05 )
		RuiSetFloat( rui, "msgAlpha", alpha )
		RuiSetFloat2( rui, "msgPos", posOffset )
		WaitFrame()
	}
	RuiDestroyIfAlive(rui)
}

string function CalculateVictimHP(float damage, entity victim)
{
	if (!GetConVarBool("comp_hud_damage_indicator_victim_hp"))
		return ""
	int totalhealth = int(victim.GetHealth() - damage);
	totalhealth = totalhealth < 0 ? 0 : totalhealth;
	return format( "\n%i HP", totalhealth)
}

void function Thread_StackingDamageFlyoutText()
{
	// outgoing damage text
	stackingOutgoingDamageTextRui = RuiCreate( $"ui/cockpit_console_text_top_left.rpak", clGlobal.topoCockpitHudPermanent, RUI_DRAW_COCKPIT, 1)
	RuiSetInt( stackingOutgoingDamageTextRui, "maxLines", 1 );
	RuiSetInt( stackingOutgoingDamageTextRui, "lineNum", 1 );
	RuiSetFloat2( stackingOutgoingDamageTextRui, "msgPos", GetConVarFloat2("comp_hud_damage_indicator_pos") )
	RuiSetFloat( stackingOutgoingDamageTextRui, "thicken", 0.0 )
	RuiSetFloat( stackingOutgoingDamageTextRui, "msgFontSize", 36.0)
	RuiSetFloat3( stackingOutgoingDamageTextRui, "msgColor", GetConVarFloat3("comp_hud_damage_indicator_color") / 255.0 )
	RuiSetFloat( stackingOutgoingDamageTextRui, "msgAlpha", 0.9 )
	RuiSetFloat( stackingOutgoingDamageTextRui, "thicken", 0.0 )

	// incoming damage text
	stackingIncomingDamageTextRui = RuiCreate( $"ui/cockpit_console_text_top_left.rpak", clGlobal.topoTitanCockpitHud, RUI_DRAW_COCKPIT, 1)
	RuiSetInt( stackingIncomingDamageTextRui, "maxLines", 1 );
	RuiSetInt( stackingIncomingDamageTextRui, "lineNum", 0 );
	RuiSetFloat2( stackingIncomingDamageTextRui, "msgPos", INCOMING_DAMAGE_TEXT_BEGIN_POS )
	RuiSetFloat( stackingIncomingDamageTextRui, "thicken", 0.0 )
	RuiSetFloat( stackingIncomingDamageTextRui, "msgFontSize", 40.0)
	RuiSetFloat3( stackingIncomingDamageTextRui, "msgColor", GetConVarFloat3("comp_hud_incoming_damage_indicator_color") / 255.0 )
	RuiSetFloat( stackingIncomingDamageTextRui, "msgAlpha", 0.9 )
	RuiSetFloat( stackingIncomingDamageTextRui, "thicken", 0.0 )

	while (true)
	{
		if (IsValid(GetLocalViewPlayer()))
		{
			INCOMING_DAMAGE_TEXT_STACKING_FADE_TIME = GetConVarFloat("comp_hud_incoming_damage_indicator_fade_time")
			INCOMING_DAMAGE_TEXT_STACKING_DURATION = GetConVarFloat("comp_hud_incoming_damage_indicator_duration")
			INCOMING_DAMAGE_TEXT_STACKING_STAY_TIME = INCOMING_DAMAGE_TEXT_STACKING_DURATION - INCOMING_DAMAGE_TEXT_STACKING_FADE_TIME

			OUTGOING_DAMAGE_TEXT_STACKING_FADE_TIME = GetConVarFloat("comp_hud_damage_indicator_fade_time")
			OUTGOING_DAMAGE_TEXT_STACKING_DURATION = GetConVarFloat("comp_hud_damage_indicator_duration")
			OUTGOING_DAMAGE_TEXT_STACKING_STAY_TIME = OUTGOING_DAMAGE_TEXT_STACKING_DURATION - OUTGOING_DAMAGE_TEXT_STACKING_FADE_TIME

			if (OUTGOING_DAMAGE_TEXT_STACKING_STAY_TIME < 0)
			{
				OUTGOING_DAMAGE_TEXT_STACKING_STAY_TIME = 0
				SetConVarFloat("comp_hud_damage_indicator_duration", OUTGOING_DAMAGE_TEXT_STACKING_DURATION)
			}

			// outgoing damage text update
			float timeSinceLastHit = Time() - lastOutgoingDamageTextHitTime
			float alpha = GraphCapped( timeSinceLastHit - OUTGOING_DAMAGE_TEXT_STACKING_STAY_TIME, 0, OUTGOING_DAMAGE_TEXT_STACKING_FADE_TIME, 0.9, 0)
			RuiSetFloat( stackingOutgoingDamageTextRui, "msgAlpha", alpha )

			if (clGlobal.isMenuOpen && !isMenuOpen)
			{
				isMenuOpen = true
				thread MenuOpen()
			}
			else if (!clGlobal.isMenuOpen) isMenuOpen = false
			// incoming damage text update

			timeSinceLastHit = Time() - lastIncomingDamageTextHitTime
			alpha = GraphCapped( timeSinceLastHit - INCOMING_DAMAGE_TEXT_STACKING_STAY_TIME, 0, INCOMING_DAMAGE_TEXT_STACKING_FADE_TIME, 0.9, 0)
			RuiSetFloat( stackingIncomingDamageTextRui, "msgAlpha", alpha )
			switch (GetLocalViewPlayer().GetPlayerModHealth())
			{
				case 12500:
				case 9000:
					// 5 segments - 1500 per segment for BT, 2500 for legion/scorch
					RuiSetFloat2( stackingIncomingDamageTextRui, "msgPos", INCOMING_DAMAGE_TEXT_BEGIN_POS + <0.035, 0, 0> )
					break;
				case 10000:
					RuiSetFloat2( stackingIncomingDamageTextRui, "msgPos", INCOMING_DAMAGE_TEXT_BEGIN_POS )
					break;
				case 7500:
					RuiSetFloat2( stackingIncomingDamageTextRui, "msgPos", INCOMING_DAMAGE_TEXT_BEGIN_POS - <0.035, 0, 0> )
					break;
			}
		}

		WaitFrame()
	}
}

void function MenuOpen()
{
	bool isCrit = false
	while (clGlobal.isMenuOpen)
	{
		accumulatedIncomingDamage = (accumulatedIncomingDamage + 50) % 250
		accumulatedOutgoingDamage = (accumulatedOutgoingDamage + 50) % 250
		if (IsValid(GetLocalClientPlayer()))
		{
			entity player = GetLocalViewPlayer()
			lastIncomingDamageTextHitTime = Time()
			lastOutgoingDamageTextHitTime = Time()
			isCrit = !isCrit
			RuiSetFloat3( stackingOutgoingDamageTextRui, "msgColor", <0.9, 0.9, 0.9> )
			float msgFontSize = GraphCapped( accumulatedOutgoingDamage, 50, 200, 36, 48 )
			RuiSetFloat( stackingOutgoingDamageTextRui, "msgFontSize", msgFontSize)
			RuiSetFloat2( stackingOutgoingDamageTextRui, "msgPos", GetConVarFloat2("comp_hud_damage_indicator_pos") - <0, GraphCapped(msgFontSize, 36, 72, 0.0, 0.05), 0> )
			RuiSetString( stackingOutgoingDamageTextRui, "msgText", accumulatedOutgoingDamage.tointeger().tostring() )
			if (accumulatedIncomingDamage > 1)
			{
				RuiSetString( stackingIncomingDamageTextRui, "msgText", format( "-%i", int( accumulatedIncomingDamage ) ) )
			}
		}
		wait max(GetConVarFloat("comp_hud_damage_indicator_duration"), GetConVarFloat("comp_hud_incoming_damage_indicator_duration"))
	}
	isMenuOpen = false
}

void function PlayerDidDamageText( float damage, vector damagePosition, entity victim, bool isCrit, bool isIneffective )
{
	if (!GetConVarBool("comp_hud_damage_indicator")) return
	if (Time() - lastOutgoingDamageTextHitTime > GetConVarFloat("comp_hud_damage_indicator_duration"))
	{
		// Reset stack
		accumulatedOutgoingDamage = damage
	}
	else
	{
		// Add to stack
		accumulatedOutgoingDamage += damage
	}
	SetOutgoingDamageTextRui(damage, isCrit, victim)
	thread AddSmallDamageTextFlyout( damage, isCrit, victim )

	lastOutgoingDamageTextHitTime = Time()
}

vector function GetDamageTextColor( bool isCrit, entity victim) {
	return ( isCrit ? GetConVarFloat3("comp_hud_damage_indicator_color_crit") : GetConVarFloat3("comp_hud_damage_indicator_color") ) / 255.0
}